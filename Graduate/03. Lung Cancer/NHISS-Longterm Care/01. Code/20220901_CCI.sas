libname raw "/userdata20/rroom029/data_source/user_data";/*raw data*/
libname db "/userdata20/rroom029/data_out/data_store"; 
libname db_out "/userdata20/rroom029/data_out/data_out";

/*T20 + T40으로 DB.LTC_FULLDAT 환자들의 진단 전 2년간 상병불러오기 */
%MACRO TG_PRE2YR_T20;
%DO YR=2008 %TO 2020;
%DO YM=%EVAL(&YR.-3)12 %TO &YR.12; /*암 진단 두 해 전 부터 진단해까지의 진료 데이터 (1개월 여유 둬서 12월 부터)*/
%IF %SUBSTR(&YM,5)=13 %THEN %LET YM=%EVAL(%SUBSTR(&YM,1,4)+1)01;
%IF &YM.=%EVAL(&YR.-3)12 %THEN %DO; %LET CREATE_OR_INSERT=CREATE TABLE; %LET AS_YN=AS; %END;
%ELSE %DO; %LET CREATE_OR_INSERT=INSERT INTO; %LET AS_YN=; %END;
PROC SQL; &CREATE_OR_INSERT. TG_CCI_T20_&YR.  &AS_YN. 
SELECT  A.INDI_DSCM_NO,A.C_DATE, B.CMN_KEY, B.MDCARE_STRT_DT, B.FORM_CD
FROM DB.TG_BFC_&YR. AS A 
INNER JOIN RAW.T20_&YM. AS B
ON A.INDI_DSCM_NO=B.INDI_DSCM_NO
WHERE B.ED_RC_TOT_AMT^=. AND B.ED_RC_TOT_AMT^=0 AND B.FORM_CD IN ('02','03')
AND INTNX('YEAR',A.C_DATE,-2,'S')<=INPUT(B.MDCARE_STRT_DT,yymmdd8.)<= INTNX('DAY',A.C_DATE,-30) ; /*진단일 2년전~30일전 진료기록만*/
; QUIT;
%END; %END; %MEND TG_PRE2YR_T20;
%TG_PRE2YR_T20;

%MACRO TG_PRE2YR_T40;
%DO YR=2008 %TO 2020;
%DO YM=%EVAL(&YR.-3)12 %TO &YR.12; /*암 진단 두 해 전 부터 진단해까지의 진료 데이터 붙이기 (1개월 여유 둬서 12월 부터)*/
%IF %SUBSTR(&YM,5)=13 %THEN %LET YM=%EVAL(%SUBSTR(&YM,1,4)+1)01;
%IF &YM.=%EVAL(&YR.-3)12 %THEN %DO; %LET CREATE_OR_INSERT=CREATE TABLE; %LET AS_YN=AS; %END;
%ELSE %DO; %LET CREATE_OR_INSERT=INSERT INTO; %LET AS_YN=; %END;
PROC SQL; &CREATE_OR_INSERT. TG_CCI_T40_&YR.  &AS_YN. 
SELECT DISTINCT B.CMN_KEY, B.MCEX_SICK_SYM, B.SICK_CLSF_TYPE
FROM TG_CCI_T20_&YR. AS A 
INNER JOIN RAW.T40_&YM. AS B
ON A.CMN_KEY=B.CMN_KEY
WHERE B.SICK_CLSF_TYPE^='3'
; QUIT;
%END;  %END; %MEND TG_PRE2YR_T40;
%TG_PRE2YR_T40;


%MACRO TG_PRE2YR_T2040;
%DO YR=2008 %TO 2020;
PROC SORT DATA=TG_CCI_T20_&YR.; BY CMN_KEY; RUN;
PROC SORT DATA=TG_CCI_T40_&YR.; BY CMN_KEY; RUN;
DATA TG_CMI_T2040_&YR.; MERGE TG_CCI_T20_&YR. (IN=A) TG_CCI_T40_&YR. (IN=B); BY CMN_KEY; IF A=1 AND B=1; RUN;
%END; %MEND TG_PRE2YR_T2040; %TG_PRE2YR_T2040;

DATA TG_CCI_T2040; SET TG_CMI_T2040_2008-TG_CMI_T2040_2020; RUN;


DATA DB.TG_COMO(DROP=MSICK3 MSICK4); SET TG_CCI_T2040;
COMO_DATE = INPUT(MDCARE_STRT_DT,yymmdd8.);
MSICK3 = SUBSTR(MCEX_SICK_SYM,1,3);
MSICK4 = SUBSTR(MCEX_SICK_SYM,1,4);
/*(1) 심근경색 */ IF MSICK3 IN ('I21','I22') OR MSICK4='I252' THEN CCI_1=1;
/*(2) 심부전   */ IF MSICK3 IN ('I43','I50') OR MSICK4 IN ('I099','I110','I130','I132','I255','I420') OR 'I425' <= MSICK4 <= 'I429' THEN CCI_2=1;
/*(3) 말초혈관질환*/ IF MSICK3 IN ('I70','I71') OR MSICK4 IN ('I731','I738','I739','I771','I790','I792','K551','K558','K559','Z958','Z959') THEN CCI_3=1;
/*(4) 뇌혈관질환   */ IF MSICK3 IN ('G45','G46') OR 'I60' <= MSICK3 <= 'I69' OR MSICK4 IN ('H340') THEN CCI_4=1;
/*(5) 치매*/ IF MSICK3 IN ('F00','F01','F02','F03','G30') OR MSICK4 IN ('F051','G311') THEN CCI_5=1;
/*(6) 만성폐질환*/ IF 'J40' <= MSICK3 <='J47' OR 'J60' <= MSICK3 <= 'J67' OR MSICK4 IN ('I278','I279','J684','J701','J703') THEN CCI_6=1;
/*(7) 류마티스성 질환*/ IF MSICK3 IN ('M05','M06','M32','M33','M34') OR MSICK4 IN ('M315','M351','M353','M360') THEN CCI_7=1; 
/*(8) 소화성 궤양증*/ IF 'K25' <= MSICK3 <= 'K28' THEN CCI_8=1;
/*(9) 가벼운 간질환*/IF MSICK3 IN ('B18','K73','K74') OR MSICK4 IN ('K700','K701','K702','K703','K709','K713','K714','K715','K717','K760','K762','K763','K764','K768','K769','Z944') THEN CCI_9=1;
/*(10) 당뇨(만성합병증외)*/ IF MSICK4 IN ('E100','E101','E106','E108','E109','E110','E116','E118','E119','E120','E121','E126','E128','E129','E130','E131','E136','E138','E139','E140','E141','E146','E148','E149') THEN CCI_10=1;
/*(11) 당뇨(만성합병증)*/ IF MSICK4 IN ('E102','E103','E104','E105','E107','E112','E113','E114','E115','E117','E122','E123','E124','E125','E127','E132','E133','E134','E135','E137','E142','E143','E144','E145','E147') THEN CCI_11=1;
/*(12) 편마비*/ IF MSICK3 IN ('G81','G82') OR MSICK4 IN ('G041','G114','G801','G802','G830','G831','G832','G833','G834','G839') THEN CCI_12=1;
/*(13) 신장질환*/ IF MSICK3 IN ('N18','N19') OR MSICK4 IN ('I120','I131','N250','Z940','Z992')  OR 'N032' <= MSICK4 <= 'N037' OR 'N052' <= MSICK4 <= 'N057' OR 'Z490' <= MSICK4 <= 'Z492' THEN CCI_13=1;
/*(14) 악성종양*/ IF 'C00' <= MSICK3 <= 'C26' OR 'C30' <= MSICK3 <= 'C34' OR 'C37' <= MSICK3 <= 'C41' OR 'C45' <= MSICK3 <= 'C58' OR 'C60' <= MSICK3 <= 'C76' OR 'C81' <= MSICK3 <= 'C85' OR 'C90' <= MSICK3 <= 'C97' OR MSICK3 IN ('C43','C88') THEN CCI_14=1;
/*(15) 심각한 간질환*/ IF MSICK4 IN ('I850','I859','I864','I982','K704','K711','K721','K729','K765','K766','K767') THEN CCI_15=1;
/*(16) 전이성고형종양*/ IF 'C77' <= MSICK3 <= 'C80' THEN CCI_16=1;
/*(17) 에이즈*/ IF MSICK3 IN ('B20','B21','B22','B24') THEN CCI_17=1;
RUN;

/*입원, 외래 각 데이터셋*/
DATA TG_COMO_IN ; SET DB.TG_COMO; WHERE FORM_CD ='02'; RUN;
DATA TG_COMO_OUT ; SET DB.TG_COMO; WHERE FORM_CD ='03'; RUN;
PROC SORT DATA=TG_COMO_OUT; BY INDI_DSCM_NO COMO_DATE; RUN;


%MACRO INDI_CCI;
%DO N=1 %TO 17;
/*[1]질병별 입원 1회 이상 환자*/
PROC SQL; CREATE TABLE COMO_INDI_IN_CCI_&N. AS SELECT DISTINCT INDI_DSCM_NO 
FROM TG_COMO_IN WHERE CCI_&N.=1 ORDER BY INDI_DSCM_NO; QUIT;

/*[2]질병별 외래 2번 이상 간격 30일 이상 환자*/
DATA COMO_OUT_DIFF_CCI_&N.; SET TG_COMO_OUT; BY INDI_DSCM_NO;
DIFF1=DIF(COMO_DATE); /*외래 간 간격 계산*/ IF FIRST.INDI_DSCM_NO THEN DO; DIFF1=.; N=1; END; ELSE N+1;
WHERE CCI_&N=1; RUN;
PROC SQL; CREATE TABLE COMO_OUT_DIFFSUM_CCI_&N. AS SELECT INDI_DSCM_NO, SUM(DIFF1) AS DIFFSUM
FROM COMO_OUT_DIFF_CCI_&N. GROUP BY INDI_DSCM_NO ORDER BY INDI_DSCM_NO; QUIT;
PROC SQL; CREATE TABLE COMO_INDI_OUT_CCI_&N. AS SELECT DISTINCT INDI_DSCM_NO 
FROM COMO_OUT_DIFFSUM_CCI_&N. WHERE DIFFSUM>=30 ORDER BY INDI_DSCM_NO; QUIT;

/*[1] 또는 [2]에 속하는 환자*/
DATA COMO_INDI_CCI_&N.; MERGE COMO_INDI_IN_CCI_&N. COMO_INDI_OUT_CCI_&N.; BY INDI_DSCM_NO; YN_CCI_&N.=1;RUN;
%END; %MEND; %INDI_CCI;


/*INDI COMO 여부*/
DATA COMO_INDI_CCI_YN; MERGE COMO_INDI_CCI_1-COMO_INDI_CCI_17; BY INDI_DSCM_NO; RUN;
PROC STDIZE DATA=COMO_INDI_CCI_YN OUT=DB.COMO_INDI_CCI_YN REPONLY MISSING=0;RUN; /*.을 0으로*/


/*가중치 합 수정 VER2 : 선택*/
DATA DB.COMO_INDI_CCI; SET DB.COMO_INDI_CCI_YN;
CCI = SUM(OF YN_CCI_1-YN_CCI_10) + 2*SUM(OF YN_CCI_11-YN_CCI_13) + 3*YN_CCI_15 + 6*YN_CCI_17; RUN;

DATA DB.TG_CCI(KEEP= INDI_DSCM_NO CCI CCI_GRP); MERGE DB.TARGET_ID DB.COMO_INDI_CCI; BY INDI_DSCM_NO; 
IF CCI='.' THEN CCI=0; 
FORMAT CCI_GRP $20.;
IF 0<=CCI<=2 THEN CCI_GRP='01. 0-2';
ELSE IF 3<=CCI THEN CCI_GRP='02. 3+';
RUN;




/*DUMMYDAT CCI DISTRIBUTION*/
PROC SQL; CREATE TABLE LTC_DUMMYCCI AS SELECT A.*,B.CCI FROM DB.LTC_DUMMY5 AS A
LEFT JOIN DB.COMO_INDI_CCI AS B ON A.INDI_DSCM_NO=B.INDI_DSCM_NO;QUIT;
DATA LTC_DUMMYCCI2; SET LTC_DUMMYCCI; 
IF CCI='' THEN CCI=0; RUN;
DATA LTC_DUMMYCCI3(DROP=AGE_GRP); SET LTC_DUMMYCCI2;
WHERE C_AGE>=65 AND C_YEAR>=2009 AND PRE_C=0;RUN;

PROC SQL; CREATE TABLE CCIDIST AS SELECT CCI, COUNT(DISTINCT INDI_DSCM_NO) AS CNT
FROM LTC_DUMMYCCI3 
GROUP BY CCI ;QUIT;



/*휴지통*/

%LET YR=2008;
%MACRO TEST;
%DO YM=%EVAL(&YR.-3)12 %TO &YR.12; /*암 진단 두 해 전 부터 진단해까지의 진료 데이터 (1개월 여유 둬서 12월 부터)*/
%IF %SUBSTR(&YM,5)=13 %THEN %LET YM=%EVAL(%SUBSTR(&YM,1,4)+1)01;
%IF &YM.=%EVAL(&YR.-3)12 %THEN %DO; %LET CREATE_OR_INSERT=CREATE TABLE; %LET AS_YN=AS; %END;
%ELSE %DO; %LET CREATE_OR_INSERT=INSERT INTO; %LET AS_YN=; %END;
PROC SQL; &CREATE_OR_INSERT. TG_CCI_T20_&YR.  &AS_YN. 
SELECT  A.INDI_DSCM_NO,A.C_DATE, B.CMN_KEY, B.MDCARE_STRT_DT, B.FORM_CD
FROM DB.TG_BFC_&YR. AS A 
INNER JOIN RAW.T20_&YM. AS B
ON A.INDI_DSCM_NO=B.INDI_DSCM_NO
WHERE B.ED_RC_TOT_AMT^=. AND B.ED_RC_TOT_AMT^=0 AND B.FORM_CD IN ('02','03')
AND INTNX('YEAR',A.C_DATE,-2,'S')<=INPUT(B.MDCARE_STRT_DT,yymmdd8.)<= INTNX('DAY',A.C_DATE,-30) ; /*진단일 2년전~30일전 진료기록만*/
; QUIT;
%END; %MEND TEST;

%TEST;


%MACRO TEST2;
%DO YM=%EVAL(&YR.-1)12 %TO &YR.12; /*암 진단 두 해 전 부터 진단해까지의 진료 데이터 붙이기 (1개월 여유 둬서 12월 부터)*/
%IF %SUBSTR(&YM,5)=13 %THEN %LET YM=%EVAL(%SUBSTR(&YM,1,4)+1)01;
%IF &YM.=%EVAL(&YR.-3)12 %THEN %DO; %LET CREATE_OR_INSERT=CREATE TABLE; %LET AS_YN=AS; %END;
%ELSE %DO; %LET CREATE_OR_INSERT=INSERT INTO; %LET AS_YN=; %END;
PROC SQL; &CREATE_OR_INSERT. TG_CCI_T40_&YR.  &AS_YN. 
SELECT DISTINCT B.CMN_KEY, B.MCEX_SICK_SYM, B.SICK_CLSF_TYPE
FROM TG_CCI_T20_&YR. AS A 
INNER JOIN RAW.T40_&YM. AS B
ON A.CMN_KEY=B.CMN_KEY
WHERE B.SICK_CLSF_TYPE^='3'
; QUIT;
%END; %MEND TEST2; %TEST2;

DATA COMO_INDI_CCI_&N._INOUT;
MERGE COMO_IN_INDI_CCI_&N. (IN=A)
			COMO_OUT_INDI_CCI_&N.  ( IN=B)
			;
BY INDI_DSCM_NO;
IF A=1 OR B=1
; RUN;


/*질병별로 입원 몇 회씩인지 COUNT
PROC SQL; CREATE TABLE COMO_CNT_IN AS
SELECT INDI_DSCM_NO, FORM_CD, SUM(CCI_1) AS CNT_CCI_1, SUM(CCI_2) AS CNT_CCI_2, SUM(CCI_3) AS CNT_CCI_3, SUM(CCI_4) AS CNT_CCI_4, SUM(CCI_5) AS CNT_CCI_5, 
SUM(CCI_6) AS CNT_CCI_6, SUM(CCI_7) AS CNT_CCI_7, SUM(CCI_8) AS CNT_CCI_8, SUM(CCI_9) AS CNT_CCI_9, SUM(CCI_10) AS CNT_CCI_10, SUM(CCI_11) AS CNT_CCI_11, 
SUM(CCI_12) AS CNT_CCI_12, SUM(CCI_13) AS CNT_CCI_13, SUM(CCI_14) AS CNT_CCI_14, SUM(CCI_15) AS CNT_CCI_15, SUM(CCI_16) AS CNT_CCI_16, SUM(CCI_17) AS CNT_CCI_17
FROM TG_COMO_IN GROUP BY INDI_DSCM_NO ; QUIT; */
/*질병별로 외래 몇 회씩인지 COUNT
PROC SQL; CREATE TABLE COMO_CNT_OUT AS
SELECT INDI_DSCM_NO, FORM_CD, SUM(CCI_1), SUM(CCI_2), SUM(CCI_3), SUM(CCI_4), SUM(CCI_5), SUM(CCI_6), SUM(CCI_7), SUM(CCI_8), SUM(CCI_9), SUM(CCI_10), SUM(CCI_11), SUM(CCI_12), SUM(CCI_13), SUM(CCI_14), SUM(CCI_15), SUM(CCI_16), SUM(CCI_17)
FROM TG_COMO_OUT GROUP BY INDI_DSCM_NO ; QUIT;*/



%LET N=1;
/*[1]질병별 입원 1회 이상 환자*/
PROC SQL; CREATE TABLE COMO_INDI_IN_CCI_&N. AS SELECT DISTINCT INDI_DSCM_NO 
FROM TG_COMO_IN WHERE CCI_&N.=1 ORDER BY INDI_DSCM_NO; QUIT;

/*[2]질병별 외래 2번 이상 간격 30일 이상 환자*/
DATA COMO_OUT_DIFF_CCI_&N.; SET TG_COMO_OUT; BY INDI_DSCM_NO;
DIFF1=DIF(COMO_DATE); /*외래 간 간격 계산*/ IF FIRST.INDI_DSCM_NO THEN DO; DIFF1=.; N=1; END; ELSE N+1;
WHERE CCI_&N=1; RUN;
PROC SQL; CREATE TABLE COMO_OUT_DIFFSUM_CCI_&N. AS SELECT INDI_DSCM_NO, SUM(DIFF1) AS DIFFSUM
FROM COMO_OUT_DIFF_CCI_&N. GROUP BY INDI_DSCM_NO ORDER BY INDI_DSCM_NO; QUIT;
PROC SQL; CREATE TABLE COMO_INDI_OUT_CCI_&N. AS SELECT DISTINCT INDI_DSCM_NO 
FROM COMO_OUT_DIFFSUM_CCI_&N. WHERE DIFFSUM>=30 ORDER BY INDI_DSCM_NO; QUIT;

/*[1] 또는 [2]에 속하는 환자*/
DATA COMO_INDI_CCI_&N.; MERGE COMO_INDI_IN_CCI_&N. COMO_INDI_OUT_CCI_&N.; BY INDI_DSCM_NO; YN_CCI_&N.=1;RUN;


/*가중치 합 최초버전*/
DATA COMO_INDI_CCI; SET DB.COMO_INDI_CCI_YN;
FORMAT CCI_GRP $20.;
CCI = SUM(OF YN_CCI_1-YN_CCI_10) + 2*SUM(OF YN_CCI_11-YN_CCI_13) + 3*YN_CCI_15 + 6*YN_CCI_17; 
IF 0=CCI THEN CCI_GRP='01. 0';
ELSE IF 1<=CCI<=2 THEN CCI_GRP='02. 1-2';
ELSE IF 3<=CCI<=4 THEN CCI_GRP='03. 3-4';
ELSE IF 5<=CCI THEN CCI_GRP='03. 5+';
RUN;

/*가중치 합 수정 VER1*/
DATA COMO_INDI_CCI_V1; SET DB.COMO_INDI_CCI_YN;
FORMAT CCI_GRP $20.;
CCI = SUM(OF YN_CCI_1-YN_CCI_10) + 2*SUM(OF YN_CCI_11-YN_CCI_13) + 3*YN_CCI_15 + 6*YN_CCI_17; 
IF 0<=CCI<=2 THEN CCI_GRP='01. 0-2';
ELSE IF 3<=CCI<=4 THEN CCI_GRP='02. 3-4';
ELSE IF 5<=CCI THEN CCI_GRP='03. 5+';
RUN;
