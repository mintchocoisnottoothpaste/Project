libname raw "/userdata20/rroom029/data_source/user_data";/*raw data*/
libname db "/userdata20/rroom029/data_out/data_store"; 
libname db_out "/userdata20/rroom029/data_out/data_out";

/*장기요양보험 신청일 및 유효기간*/
/*TARGET  대상자를 장기요양_기본 (맞춤형)데이터셋에서 추출*/
PROC SQL; CREATE TABLE DB.TARGET_LTC_TBNYBASE AS
SELECT DISTINCT A.INDI_DSCM_NO, A.PRE_C, A.C_DATE, B.*, INPUT(SUBSTR(B.APLY_YM,1,4),4.) AS APLY_YR, 
INTNX('MONTH',INPUT(B.APLY_YM,YYMMN6.),1)-1 AS APLY_YMD FORMAT=yymmdd10.,/*신청년월*/
INTNX('MONTH',INPUT(B.APLY_YM,YYMMN6.),0) AS APLY_YMD2 FORMAT=yymmdd10.,

INTNX('MONTH',INPUT(B.GRADE_JUDG_YM,YYMMN6.),1)-1 AS GRADE_JUDG_YMD FORMAT=yymmdd10.,/*판정년월*/
INTNX('MONTH',INPUT(B.GRADE_JUDG_YM,YYMMN6.),0) AS GRADE_JUDG_YMD2 FORMAT=yymmdd10.,

INPUT(RCGT_EDA_FR_DT,YYMMDD10.) AS RCGT_EDA_FR_YMD FORMAT=yymmdd10.,/*인정유효기간시작일자*/
INPUT(RCGT_EDA_TO_DT,YYMMDD10.) AS RCGT_EDA_TO_YMD FORMAT=yymmdd10./*인정유효기간종료일자*/
FROM DB.TARGET_IDBFCDTH AS A
INNER JOIN RAW.RVSN_HFVT_TBNYBASE AS B
ON A.INDI_DSCM_NO=B.INDI_DSCM_NO
; QUIT;


/*1. 진단일이 장기요양보험 유효기간 내인 경우 : 18,496명*/
DATA LTC_EDA_YN; SET DB.TARGET_LTC_TBNYBASE;
WHERE RCGT_EDA_FR_YMD<=C_DATE<=RCGT_EDA_TO_YMD; 
EDA_YN=1; EDA_GRD=GJU_RCGT_GRADE_CD; RUN; 
/*DISTINCT 체크  완료. 
PROC SQL; CREATE TABLE TEST2 AS SELECT DISTINCT INDI_DSCM_NO FROM LTC_EDA_YN ;QUIT;*/

/*1.A 진단일 이전에 신청건(이후 인정)이 존재하는 경우 */
DATA LTC_APLY_BEFC; SET DB.TARGET_LTC_TBNYBASE;
WHERE C_DATE>APLY_YMD AND GJU_RCGT_GRADE_CD IN ('1','2','3','4','5','6'); RUN;
PROC SQL; CREATE TABLE LTC_GRADE_BEFC AS SELECT INDI_DSCM_NO, 1 AS APLY_BEFC_YN, COUNT(DISTINCT INDI_SEQ) AS APLY_BEFC_N, MIN(INPUT(GJU_RCGT_GRADE_CD,1.)) AS MIN_BEFC_GRD 
FROM LTC_APLY_BEFC GROUP BY INDI_DSCM_NO; QUIT; /*2년 내 복수 건 신청자들은 받은 등급 중 최고 등급(가장 작은 숫자)으로, 6.17결정*/

/*2. 진단일 이후 2년 내에 신청건(이후 인정)이 존재하는 경우 : 40916건, 중복포함. 34458명 중복 미포함*/
DATA LTC_APLY; SET DB.TARGET_LTC_TBNYBASE;
WHERE C_DATE<=APLY_YMD AND APLY_YMD2<= INTNX('YEAR',C_DATE,2,'S')  AND GJU_RCGT_GRADE_CD IN ('1','2','3','4','5','6'); RUN;

PROC SQL; CREATE TABLE LTC_GRADE AS SELECT INDI_DSCM_NO, 1 AS APLY_YN, COUNT(DISTINCT INDI_SEQ) AS APLY_N, MIN(INPUT(GJU_RCGT_GRADE_CD,1.)) AS MIN_GRD 
FROM LTC_APLY GROUP BY INDI_DSCM_NO; QUIT; /*2년 내 복수 건 신청자들은 받은 등급 중 최고 등급(가장 작은 숫자)으로, 6.17결정*/

DATA LTC_GRADEGRP; SET LTC_GRADE; IF MIN_GRD>=4 THEN MIN_GRD_GRP=3; ELSE MIN_GRD_GRP=MIN_GRD; RUN;


/*3. 요양병원 환자분류군 : 중복 제외 9,141명*/
DATA ADJ_PATIENT; SET DB.TARGET_ADJ_2YR;
IF LTC_ADJ IN ('01. 의료최고도 (입원)','02. 의료최고도 (외박)') THEN PATIENT_GRP_NUM=1;
IF LTC_ADJ IN ('03. 의료고도 (입원)','04. 의료고도 (외박)') THEN PATIENT_GRP_NUM=2;
IF LTC_ADJ IN ('05. 의료중도','06. 의료중도 (입원)','07. 의료중도 (외박)') THEN PATIENT_GRP_NUM=3;
IF LTC_ADJ IN ('08. 의료경도 (입원)','09. 의료경도 (외박)') THEN PATIENT_GRP_NUM=4;
IF LTC_ADJ IN ('10. 선택입원 (입원)','11. 선택입원 (외박)') THEN PATIENT_GRP_NUM=5;RUN;

PROC SQL; CREATE TABLE ADJ_PATIENT_GRP AS SELECT INDI_DSCM_NO, COUNT(DISTINCT CMN_KEY) AS ADJ_N, MIN(PATIENT_GRP_NUM) AS MIN_GRP_NUM
FROM ADJ_PATIENT GROUP BY INDI_DSCM_NO; QUIT; /*2년 내 복수 건 진료자들은 받은 환자 분류군 중 최고 등급(가장 작은 숫자)으로, 6.17결정*/
/*DISTINCT 체크  완료. 
PROC SQL; CREATE TABLE TEST2 AS SELECT DISTINCT INDI_DSCM_NO FROM ADJ_PATIENT_GRP ;QUIT;*/

DATA ADJ_PATIENT_MINGRP; SET ADJ_PATIENT_GRP;
IF MIN_GRP_NUM=1 THEN MIN_GRP='01. 의료최고도';
IF MIN_GRP_NUM=2 THEN MIN_GRP='02. 의료고도';
IF MIN_GRP_NUM=3 THEN MIN_GRP='03. 의료중도';
IF MIN_GRP_NUM=4 THEN MIN_GRP='04. 의료경도';
IF MIN_GRP_NUM=5 THEN MIN_GRP='05. 선택입원';RUN;


DATA DB.TARGET_APLYGRDGRP(KEEP=INDI_DSCM_NO PRE_C C_DATE EDA_YN EDA_GRD APLY_YN MIN_GRD MIN_GRD_GRP APLY_BEFC_YN MIN_BEFC_GRD MIN_GRP) ; 
MERGE DB.TARGET_IDBFCDTH (IN=A) LTC_EDA_YN LTC_GRADEGRP LTC_GRADE_BEFC ADJ_PATIENT_MINGRP;
BY INDI_DSCM_NO; IF A=1; RUN;



/*****************************************************/



/*LTC_DUMMY에 환자분류군 정보 붙이기*/
PROC SQL; CREATE TABLE LTC_DUMMY3 AS SELECT A.*,B.* FROM LTC_DUMMY2 AS A
LEFT JOIN PGRP_MERGE AS B ON A.INDI_DSCM_NO=B.INDI_DSCM_NO;QUIT;


DATA LTC_DUMMY4; SET LTC_DUMMY3;
IF C_YEAR IN (2009,2010,2011) THEN C_YEAR_GRP="01. 2009-2011";
IF C_YEAR IN (2012,2013,2014) THEN C_YEAR_GRP="02. 2012-2014";
IF C_YEAR IN (2015,2016,2017) THEN C_YEAR_GRP="03. 2015-2017";
IF C_YEAR IN (2018,2019,2020) THEN C_YEAR_GRP="04. 2018-2020";

IF OP_2YR_YN=1 OR CT_2YR_YN=1 OR RT_2YR_YN=1 THEN TRT_ANY_YN=1; ELSE TRT_ANY_YN=0;
IF OP_2YR_YN=. THEN OP_2YR_YN=0;
IF CT_2YR_YN=. THEN CT_2YR_YN=0;
IF RT_2YR_YN=. THEN RT_2YR_YN=0;
RUN;

DATA DB.LTC_DUMMY5; SET LTC_DUMMY4;
FORMAT RESID_GRP $20.;
IF SIDO_CD IN ('11','26','27','28','29','30','31') THEN RESID_GRP='01.METRO';
ELSE RESID_GRP='02.NON-METRO'; RUN;

/*[4] Charlson comorbidity index 구해서 붙이기*/
PROC SQL; CREATE TABLE LTC_DUMMY6 AS SELECT A.*,B.CCI_GRP FROM DB.LTC_DUMMY5 AS A
LEFT JOIN DB.COMO_INDI_CCI AS B ON A.INDI_DSCM_NO=B.INDI_DSCM_NO;QUIT;
DATA DB.LTC_DUMMYFULL; RENAME MAIN_USE_2YR=LTC_MAIN_2YR; SET LTC_DUMMY6; 
IF CCI_GRP='' THEN CCI_GRP='01. 0-2'; IF DEATH=0 THEN SURVIVAL=1; ELSE IF DEATH=1 THEN SURVIVAL=0;RUN;


/*DUMMY TABLE용 데이터셋 구축 완료*/
/*대상자 : 2009년 이후 진단자, 65세 이상, 타암 기진단자 제외*/
DATA DB.LTC_DUMMYDAT(DROP=AGE_GRP); SET DB.LTC_DUMMYFULL;
WHERE C_AGE>=65 AND C_YEAR>=2009 AND PRE_C=0;RUN;

/*DATA TEST3; SETDB.LTC_DUMMYDAT; WHERE INC_QUINTILE IS NULL; RUN; 소득분위 결측인 자 3977명*/


libname DMY_FQ "/userdata20/rroom029/data_out/data_out/DUMMY_FREQ";

/*Table1. by LTC Service*/
/*Table 1. Baseline characteristics of the study participants by Long-term Care Service*/
/*22.06.17 PGRP, GRD에서 MIN(가장 높은 등급) 사용하기로*/
%LET VARLIST= SEX_TYPE/E_AGE_GRP/INC_QUINTILE/RESID_GRP/SIDO_CD/CCI_GRP/C_YEAR_GRP/SURVIVAL/MIN_GRD/MIN_PGRP/TRT_ANY_YN/OP_2YR_YN/CT_2YR_YN/RT_2YR_YN;
%LET N_VAR=14;
%MACRO FREQ(COND);
%DO i=1 %TO &N_VAR.; %LET VAR = %SCAN(&VARLIST., &i, "/");
	PROC FREQ DATA=DB.LTC_DUMMYDAT NOPRINT; TABLES LTC_MAIN_2YR*&VAR. /OUT=FREQ&i. (DROP=PERCENT); WHERE &COND.; RUN;
%END;
%MEND FREQ; 
%FREQ(); 
DATA DMY_FQ.TB1_1; SET FREQ1-FREQ&N_VAR.; WHERE LTC_MAIN_2YR='01. 요양병원'; RUN;
DATA DMY_FQ.TB1_2; SET FREQ1-FREQ&N_VAR.; WHERE LTC_MAIN_2YR='02. 시설'; RUN;
DATA DMY_FQ.TB1_3; SET FREQ1-FREQ&N_VAR.; WHERE LTC_MAIN_2YR='03. 재가'; RUN;
DATA DMY_FQ.TB1_4; SET FREQ1-FREQ&N_VAR.; WHERE LTC_MAIN_2YR='04. NONE'; RUN;

/*Median length of follow up*/
%MACRO MST(i,COND,GRP);
PROC LIFETEST DATA=DB.LTC_DUMMYDAT NOTABLE NOPRINT OUTSURV=RES; TIME SURVTIME*DEATH(0);WHERE &COND.; RUN;
PROC SQL; CREATE TABLE TMP0&i. AS SELECT * FROM RES WHERE SURVIVAL>=0.5 ORDER BY SURVIVAL ; QUIT; 
DATA TMP1&i.(KEEP=SURVTIME SURVIVAL); SET TMP0&i.(OBS=1); RUN;
DATA MST&i. ; SET TMP1&i.; FORMAT VAR $20.; VAR=&GRP. ;RUN; 
%MEND;
%MST(i=0, GRP='00. 전체');
%MST(i=1,COND=LTC_MAIN_2YR='01. 요양병원', GRP='01. 요양병원');
%MST(i=2,COND=LTC_MAIN_2YR='02. 시설', GRP='02. 시설');
%MST(i=3,COND=LTC_MAIN_2YR='03. 재가', GRP='03. 재가');
%MST(i=4,COND=LTC_MAIN_2YR='04. NONE', GRP='04. NONE');
DATA DMY_FQ.TB1_MST; SET MST0-MST4; RUN;

/*Table 1a. Baseline characteristics of the study participants by Long-term Care Service in Treatment group*/
%FREQ(COND= TRT_ANY_YN=1); 
DATA DMY_FQ.TB1A_1; SET FREQ1-FREQ&N_VAR.; WHERE LTC_MAIN_2YR='01. 요양병원'; RUN;
DATA DMY_FQ.TB1A_2; SET FREQ1-FREQ&N_VAR.; WHERE LTC_MAIN_2YR='02. 시설'; RUN;
DATA DMY_FQ.TB1A_3; SET FREQ1-FREQ&N_VAR.; WHERE LTC_MAIN_2YR='03. 재가'; RUN;
DATA DMY_FQ.TB1A_4; SET FREQ1-FREQ&N_VAR.; WHERE LTC_MAIN_2YR='04. NONE'; RUN;
/*Median length of follow up*/
%MST(i=0,COND=TRT_ANY_YN=1, GRP='00. 전체');
%MST(i=1,COND=TRT_ANY_YN=1 AND LTC_MAIN_2YR='01. 요양병원', GRP='01. 요양병원');
%MST(i=2,COND=TRT_ANY_YN=1 AND LTC_MAIN_2YR='02. 시설', GRP='02. 시설');
%MST(i=3,COND=TRT_ANY_YN=1 AND LTC_MAIN_2YR='03. 재가', GRP='03. 재가');
%MST(i=4,COND=TRT_ANY_YN=1 AND LTC_MAIN_2YR='04. NONE', GRP='04. NONE');
DATA DMY_FQ.TB1A_MST; SET MST0-MST4; RUN;

/*Table 1b. Baseline characteristics of the study participants by Long-term Care Service in No-treatment group*/
%FREQ(COND= TRT_ANY_YN=0); 
DATA DMY_FQ.TB1B_1; SET FREQ1-FREQ&N_VAR.; WHERE LTC_MAIN_2YR='01. 요양병원'; RUN;
DATA DMY_FQ.TB1B_2; SET FREQ1-FREQ&N_VAR.; WHERE LTC_MAIN_2YR='02. 시설'; RUN;
DATA DMY_FQ.TB1B_3; SET FREQ1-FREQ&N_VAR.; WHERE LTC_MAIN_2YR='03. 재가'; RUN;
DATA DMY_FQ.TB1B_4; SET FREQ1-FREQ&N_VAR.; WHERE LTC_MAIN_2YR='04. NONE'; RUN;
/*Median length of follow up*/
%MST(i=0,COND=TRT_ANY_YN=0, GRP='00. 전체');
%MST(i=1,COND=TRT_ANY_YN=0 AND LTC_MAIN_2YR='01. 요양병원', GRP='01. 요양병원');
%MST(i=2,COND=TRT_ANY_YN=0 AND LTC_MAIN_2YR='02. 시설', GRP='02. 시설');
%MST(i=3,COND=TRT_ANY_YN=0 AND LTC_MAIN_2YR='03. 재가', GRP='03. 재가');
%MST(i=4,COND=TRT_ANY_YN=0 AND LTC_MAIN_2YR='04. NONE', GRP='04. NONE');
DATA DMY_FQ.TB1B_MST; SET MST0-MST4; RUN;


/*Table2. by Age*/
/*Table 2. Baseline characteristics of the study participants by Age*/
%LET VARLIST= LTC_MAIN_2YR/SEX_TYPE/INC_QUINTILE/RESID_GRP/SIDO_CD/CCI_GRP/C_YEAR_GRP/SURVIVAL/MIN_GRD/MIN_PGRP/TRT_ANY_YN/OP_2YR_YN/CT_2YR_YN/RT_2YR_YN;
%LET N_VAR=14;
%MACRO FREQ(COND);
%DO i=1 %TO &N_VAR.; %LET VAR = %SCAN(&VARLIST., &i, "/");
	PROC FREQ DATA=DB.LTC_DUMMYDAT NOPRINT; TABLES E_AGE_GRP*&VAR. /OUT=FREQ&i. (DROP=PERCENT); WHERE &COND.; RUN;
%END;
%MEND FREQ; 
%FREQ(); 
DATA DMY_FQ.TB2_1; SET FREQ1-FREQ&N_VAR.; WHERE E_AGE_GRP='01. 65-74'; RUN;
DATA DMY_FQ.TB2_2; SET FREQ1-FREQ&N_VAR.; WHERE E_AGE_GRP='02. 75-84'; RUN;
DATA DMY_FQ.TB2_3; SET FREQ1-FREQ&N_VAR.; WHERE E_AGE_GRP='03. 85+'; RUN;
/*Median length of follow up*/
%MST(i=0, GRP='00. 전체');
%MST(i=1,COND=E_AGE_GRP='01. 65-74',GRP='01. 65-74');
%MST(i=2,COND=E_AGE_GRP='02. 75-84',GRP='02. 75-84');
%MST(i=3,COND=E_AGE_GRP='03. 85+', GRP='03. 85+');
DATA DMY_FQ.TB2_MST; SET MST0-MST3; RUN;

/*Table 2a. Baseline characteristics of the study participants by Age in Treatment group*/
%FREQ(COND= TRT_ANY_YN=1); 
DATA DMY_FQ.TB2A_1; SET FREQ1-FREQ&N_VAR.; WHERE E_AGE_GRP='01. 65-74'; RUN;
DATA DMY_FQ.TB2A_2; SET FREQ1-FREQ&N_VAR.; WHERE E_AGE_GRP='02. 75-84'; RUN;
DATA DMY_FQ.TB2A_3; SET FREQ1-FREQ&N_VAR.; WHERE E_AGE_GRP='03. 85+'; RUN;
/*Median length of follow up*/
%MST(i=0,COND=TRT_ANY_YN=1, GRP='00. 전체');
%MST(i=1,COND=TRT_ANY_YN=1 AND E_AGE_GRP='01. 65-74', GRP='01. 65-74');
%MST(i=2,COND=TRT_ANY_YN=1 AND E_AGE_GRP='02. 75-84', GRP='02. 75-84');
%MST(i=3,COND=TRT_ANY_YN=1 AND E_AGE_GRP='03. 85+', GRP='03. 85+');
DATA DMY_FQ.TB2A_MST; SET MST0-MST3; RUN;

/*Table 2b. Baseline characteristics of the study participants by Age in No-treatment group*/
%FREQ(COND= TRT_ANY_YN=0); 
DATA DMY_FQ.TB2B_1; SET FREQ1-FREQ&N_VAR; WHERE E_AGE_GRP='01. 65-74'; RUN;
DATA DMY_FQ.TB2B_2; SET FREQ1-FREQ&N_VAR; WHERE E_AGE_GRP='02. 75-84'; RUN;
DATA DMY_FQ.TB2B_3; SET FREQ1-FREQ&N_VAR; WHERE E_AGE_GRP='03. 85+'; RUN;
/*Median length of follow up*/
%MST(i=0,COND=TRT_ANY_YN=0, GRP='00. 전체');
%MST(i=1,COND=TRT_ANY_YN=0 AND E_AGE_GRP='01. 65-74', GRP='01. 65-74');
%MST(i=2,COND=TRT_ANY_YN=0 AND E_AGE_GRP='02. 75-84', GRP='02. 75-84');
%MST(i=3,COND=TRT_ANY_YN=0 AND E_AGE_GRP='03. 85+', GRP='03. 85+');
DATA DMY_FQ.TB2B_MST; SET MST0-MST3; RUN;


/*Table3. Cox model*/
/*Table 3a. Cox proportional hazard model on the factors related to survival in the study participants in Treatment group*/
%LET COND= TRT_ANY_YN=1;
/*단변량*/
%LET VARLIST= SEX_TYPE/E_AGE_GRP/INC_QUINTILE/SIDO_CD/RESID_GRP/CCI_GRP/C_YEAR_GRP/LTC_MAIN_2YR/OP_2YR_YN/CT_2YR_YN/RT_2YR_YN;
%LET VARREFLIST='2'/'01. 65-74'/'5'/'11'/'01.METRO'/'01. 0-2'/'01. 2009-2011'/'04. NONE'/'0'/'0'/'0';
%LET N_VAR=11;
%MACRO PHREG_UNI;
%DO i=1 %TO &N_VAR.; 
	%LET VAR = %SCAN(&VARLIST., &i, "/");
	%LET VARREF = %SCAN(&VARREFLIST., &i, "/");
	PROC PHREG DATA=DB.LTC_DUMMYDAT; CLASS &VAR. (REF=&VARREF.); MODEL SURVTIME*DEATH(0)=&VAR. /RL; WHERE &COND.; RUN;
%END;
%MEND; %PHREG_UNI;
/*다변량*/ /*MULTIVARIATE에는 SIDO_CD만 넣음*/
PROC PHREG DATA=DB.LTC_DUMMYDAT;
CLASS SEX_TYPE (REF='2') E_AGE_GRP (REF='01. 65-74') INC_QUINTILE(REF='5') SIDO_CD(REF='11') CCI_GRP(REF='01. 0-2') 
C_YEAR_GRP(REF="01. 2009-2011") LTC_MAIN_2YR (REF='04. NONE') OP_2YR_YN(REF='0') CT_2YR_YN(REF='0') RT_2YR_YN(REF='0');
MODEL SURVTIME*DEATH(0)=SEX_TYPE E_AGE_GRP INC_QUINTILE SIDO_CD CCI_GRP C_YEAR_GRP LTC_MAIN_2YR OP_2YR_YN CT_2YR_YN RT_2YR_YN
/RL;WHERE &COND.; RUN;


/*Table 3b. Cox proportional hazard model on the factors related to survival in the study participants in No-treatment group*/
%LET COND= TRT_ANY_YN=0;
/*단변량*/
%LET VARLIST= SEX_TYPE/E_AGE_GRP/INC_QUINTILE/SIDO_CD/RESID_GRP/CCI_GRP/C_YEAR_GRP/LTC_MAIN_2YR;
%LET VARREFLIST='2'/'01. 65-74'/'5'/'11'/'01.METRO'/'01. 0-2'/'01. 2009-2011'/'04. NONE';
%LET N_VAR=8;
%PHREG_UNI;
/*다변량*/ 
PROC PHREG DATA=DB.LTC_DUMMYDAT;
CLASS SEX_TYPE (REF='2') E_AGE_GRP (REF='01. 65-74') INC_QUINTILE(REF='5') SIDO_CD(REF='11') CCI_GRP(REF='01. 0-2') 
C_YEAR_GRP(REF="01. 2009-2011") LTC_MAIN_2YR (REF='04. NONE') OP_2YR_YN(REF='0') CT_2YR_YN(REF='0') RT_2YR_YN(REF='0');
MODEL SURVTIME*DEATH(0)=SEX_TYPE E_AGE_GRP INC_QUINTILE SIDO_CD CCI_GRP C_YEAR_GRP LTC_MAIN_2YR 
/RL;WHERE &COND.; RUN;


/*Table 3c. Cox proportional hazard model on the factors related to survival in the study participants (전체)*/
%LET COND= ;
/*단변량: 상동*/
%PHREG_UNI;
/*다변량*/ 
PROC PHREG DATA=DB.LTC_DUMMYDAT;
CLASS SEX_TYPE (REF='2') E_AGE_GRP (REF='01. 65-74') INC_QUINTILE(REF='5') SIDO_CD(REF='11') CCI_GRP(REF='01. 0-2') 
C_YEAR_GRP(REF="01. 2009-2011") LTC_MAIN_2YR (REF='04. NONE') OP_2YR_YN(REF='0') CT_2YR_YN(REF='0') RT_2YR_YN(REF='0');
MODEL SURVTIME*DEATH(0)=SEX_TYPE E_AGE_GRP INC_QUINTILE SIDO_CD CCI_GRP C_YEAR_GRP LTC_MAIN_2YR 
/RL;WHERE &COND.; RUN;



/*Supp1. by LTCS, Age*/
/*Supplementary Table 1. Baseline characteristics of the study participants by Long-term Care Service and age*/
%LET VARLIST= SEX_TYPE/INC_QUINTILE/RESID_GRP/SIDO_CD/CCI_GRP/C_YEAR_GRP/SURVIVAL/MIN_GRD/MIN_PGRP/TRT_ANY_YN/OP_2YR_YN/CT_2YR_YN/RT_2YR_YN;
%LET N_VAR=13;
%MACRO SUPP1(TBNAME, LTCCOND, TRTCOND);
%FREQ(COND=LTC_MAIN_2YR=&LTCCOND. &TRTCOND.); 
DATA DMY_FQ.TBSP&TBNAME._1; SET FREQ1-FREQ&N_VAR.; WHERE E_AGE_GRP='01. 65-74'; RUN;
DATA DMY_FQ.TBSP&TBNAME._2; SET FREQ1-FREQ&N_VAR.; WHERE E_AGE_GRP='02. 75-84'; RUN;
DATA DMY_FQ.TBSP&TBNAME._3; SET FREQ1-FREQ&N_VAR.; WHERE E_AGE_GRP='03. 85+'; RUN;

/*Median length of follow up*/
%MST(i=0,COND=LTC_MAIN_2YR=&LTCCOND. &TRTCOND., GRP='00. 전체');
%MST(i=1,COND=LTC_MAIN_2YR=&LTCCOND. &TRTCOND. AND E_AGE_GRP='01. 65-74', GRP='01. 65-74');
%MST(i=2,COND=LTC_MAIN_2YR=&LTCCOND. &TRTCOND. AND E_AGE_GRP='02. 75-84', GRP='02. 75-84');
%MST(i=3,COND=LTC_MAIN_2YR=&LTCCOND. &TRTCOND. AND E_AGE_GRP='03. 85+', GRP='03. 85+');
DATA DMY_FQ.TBSP&TBNAME._MST; SET MST0-MST3; RUN;
%MEND;

%SUPP1(TBNAME=1_1, LTCCOND='01. 요양병원'); 
%SUPP1(TBNAME=1_2, LTCCOND='02. 시설');
%SUPP1(TBNAME=1_3, LTCCOND='03. 재가');
%SUPP1(TBNAME=1_4, LTCCOND='04. NONE');

%SUPP1(TBNAME=1A_1, LTCCOND='01. 요양병원',TRTCOND= AND TRT_ANY_YN=1);
%SUPP1(TBNAME=1A_2, LTCCOND='02. 시설',TRTCOND= AND TRT_ANY_YN=1);
%SUPP1(TBNAME=1A_3, LTCCOND='03. 재가',TRTCOND= AND TRT_ANY_YN=1);
%SUPP1(TBNAME=1A_4, LTCCOND='04. NONE',TRTCOND= AND TRT_ANY_YN=1);

%SUPP1(TBNAME=1B_1, LTCCOND='01. 요양병원',TRTCOND= AND TRT_ANY_YN=0);
%SUPP1(TBNAME=1B_2, LTCCOND='02. 시설',TRTCOND= AND TRT_ANY_YN=0);
%SUPP1(TBNAME=1B_3, LTCCOND='03. 재가',TRTCOND= AND TRT_ANY_YN=0);
%SUPP1(TBNAME=1B_4, LTCCOND='04. NONE',TRTCOND= AND TRT_ANY_YN=0);



/*Supp2a. Cox model (Age)*/
/*Supplementary Table 2A1. Cox proportional hazard model on the factors related to survival in patients aged 65-74*/
%LET COND= E_AGE_GRP='01. 65-74';
/*단변량*/
%LET VARLIST= SEX_TYPE/INC_QUINTILE/SIDO_CD/RESID_GRP/CCI_GRP/C_YEAR_GRP/LTC_MAIN_2YR/OP_2YR_YN/CT_2YR_YN/RT_2YR_YN;
%LET VARREFLIST='2'/'5'/'11'/'02.NON-METRO'/'01. 0'/'01. 2009-2011'/'04. NONE'/'0'/'0'/'0';
%LET N_VAR=10;
%PHREG_UNI;
/*다변량*/ 
PROC PHREG DATA=DB.LTC_DUMMYDAT;
CLASS SEX_TYPE (REF='2') INC_QUINTILE(REF='5') RESID_GRP(REF='02.NON-METRO') CCI_GRP(REF='01. 0') 
C_YEAR_GRP(REF="01. 2009-2011") LTC_MAIN_2YR (REF='04. NONE') OP_2YR_YN(REF='0') CT_2YR_YN(REF='0') RT_2YR_YN(REF='0');
MODEL SURVTIME*DEATH(0)=SEX_TYPE INC_QUINTILE RESID_GRP CCI_GRP C_YEAR_GRP LTC_MAIN_2YR OP_2YR_YN CT_2YR_YN RT_2YR_YN
/RL;WHERE &COND.; RUN;


/*Supplementary Table 2A2. Cox proportional hazard model on the factors related to survival in patients aged 75-84*/
%LET COND= E_AGE_GRP='02. 75-84';
/*단변량*/
%PHREG_UNI;
/*다변량*/
PROC PHREG DATA=DB.LTC_DUMMYDAT;
CLASS SEX_TYPE (REF='2') INC_QUINTILE(REF='5') RESID_GRP(REF='02.NON-METRO') CCI_GRP(REF='01. 0') 
C_YEAR_GRP(REF="01. 2009-2011") LTC_MAIN_2YR (REF='04. NONE') OP_2YR_YN(REF='0') CT_2YR_YN(REF='0') RT_2YR_YN(REF='0');
MODEL SURVTIME*DEATH(0)=SEX_TYPE INC_QUINTILE RESID_GRP CCI_GRP C_YEAR_GRP LTC_MAIN_2YR OP_2YR_YN CT_2YR_YN RT_2YR_YN
/RL;WHERE &COND.; RUN;


/*Supplementary Table 2A3. Cox proportional hazard model on the factors related to survival in patients aged 85+*/
%LET COND= E_AGE_GRP='03. 85+';
/*단변량*/
%PHREG_UNI;
/*다변량*/
PROC PHREG DATA=DB.LTC_DUMMYDAT;
CLASS SEX_TYPE (REF='2')  INC_QUINTILE(REF='5') RESID_GRP(REF='02.NON-METRO') CCI_GRP(REF='01. 0') 
C_YEAR_GRP(REF="01. 2009-2011") LTC_MAIN_2YR (REF='04. NONE') OP_2YR_YN(REF='0') CT_2YR_YN(REF='0') RT_2YR_YN(REF='0');
MODEL SURVTIME*DEATH(0)=SEX_TYPE INC_QUINTILE RESID_GRP CCI_GRP C_YEAR_GRP LTC_MAIN_2YR OP_2YR_YN CT_2YR_YN RT_2YR_YN
/RL;WHERE &COND.; RUN;



/*Supp2b. Cox model (LTCS)*/
/*Supplementary Table 2B1. Cox proportional hazard model on the factors related to survival in Geriatric Hospital participants*/
%LET COND= LTC_MAIN_2YR='01. 요양병원'; 
/*단변량*/
%LET VARLIST= SEX_TYPE/E_AGE_GRP/INC_QUINTILE/SIDO_CD/RESID_GRP/CCI_GRP/C_YEAR_GRP/OP_2YR_YN/CT_2YR_YN/RT_2YR_YN;
%LET VARREFLIST='2'/'01. 65-74'/'5'/'11'/'02.NON-METRO'/'01. 0'/'01. 2009-2011'/'0'/'0'/'0';
%LET N_VAR=10;
%PHREG_UNI;
/*다변량*/
PROC PHREG DATA=DB.LTC_DUMMYDAT;
CLASS SEX_TYPE (REF='2') E_AGE_GRP (REF='01. 65-74') INC_QUINTILE(REF='5') RESID_GRP(REF='02.NON-METRO') CCI_GRP(REF='01. 0') 
C_YEAR_GRP(REF="01. 2009-2011") OP_2YR_YN(REF='0') CT_2YR_YN(REF='0') RT_2YR_YN(REF='0');
MODEL SURVTIME*DEATH(0)=SEX_TYPE E_AGE_GRP INC_QUINTILE RESID_GRP CCI_GRP C_YEAR_GRP  OP_2YR_YN CT_2YR_YN RT_2YR_YN
/RL;WHERE &COND.; RUN;


/*Supplementary Table 2B2. Cox proportional hazard model on the factors related to survival in LTCI (Assisted living residences, Nursing home)*/
%LET COND= LTC_MAIN_2YR='02. 시설'; 
/*단변량*/ %PHREG_UNI;
/*다변량*/
PROC PHREG DATA=DB.LTC_DUMMYDAT;
CLASS SEX_TYPE (REF='2') E_AGE_GRP (REF='01. 65-74') INC_QUINTILE(REF='5') RESID_GRP(REF='02.NON-METRO') CCI_GRP(REF='01. 0') 
C_YEAR_GRP(REF="01. 2009-2011") OP_2YR_YN(REF='0') CT_2YR_YN(REF='0') RT_2YR_YN(REF='0');
MODEL SURVTIME*DEATH(0)=SEX_TYPE E_AGE_GRP INC_QUINTILE RESID_GRP CCI_GRP C_YEAR_GRP  OP_2YR_YN CT_2YR_YN RT_2YR_YN
/RL;WHERE &COND.; RUN;


/*Supplementary Table 2B3. Cox proportional hazard model on the factors related to survival in LTCI (Home-care)*/
%LET COND= LTC_MAIN_2YR='03. 재가'; 
/*단변량*/ %PHREG_UNI;
/*다변량*/
PROC PHREG DATA=DB.LTC_DUMMYDAT;
CLASS SEX_TYPE (REF='2') E_AGE_GRP (REF='01. 65-74') INC_QUINTILE(REF='5') RESID_GRP(REF='02.NON-METRO') CCI_GRP(REF='01. 0') 
C_YEAR_GRP(REF="01. 2009-2011") OP_2YR_YN(REF='0') CT_2YR_YN(REF='0') RT_2YR_YN(REF='0');
MODEL SURVTIME*DEATH(0)=SEX_TYPE E_AGE_GRP INC_QUINTILE RESID_GRP CCI_GRP C_YEAR_GRP  OP_2YR_YN CT_2YR_YN RT_2YR_YN
/RL;WHERE &COND.; RUN;



/*Supp2C. Cox model (TRT*AGE)*/
/*Supplementary Table 2C1. Cox proportional hazard model on the factors related to survival in patients in treatment group aged 65-74*/
%LET COND= TRT_ANY_YN=1 AND E_AGE_GRP='01. 65-74';
/*단변량*/ 
%LET VARLIST= SEX_TYPE/INC_QUINTILE/SIDO_CD/RESID_GRP/CCI_GRP/C_YEAR_GRP/LTC_MAIN_2YR/OP_2YR_YN/CT_2YR_YN/RT_2YR_YN;
%LET VARREFLIST='2'/'5'/'11'/'02.NON-METRO'/'01. 0'/'01. 2009-2011'/'04. NONE'/'0'/'0'/'0';
%LET N_VAR=10;
%PHREG_UNI;
/*다변량*/
PROC PHREG DATA=DB.LTC_DUMMYDAT;
CLASS SEX_TYPE (REF='2') INC_QUINTILE(REF='5') RESID_GRP(REF='02.NON-METRO') CCI_GRP(REF='01. 0') 
C_YEAR_GRP(REF="01. 2009-2011") LTC_MAIN_2YR (REF='04. NONE') OP_2YR_YN(REF='0') CT_2YR_YN(REF='0') RT_2YR_YN(REF='0');
MODEL SURVTIME*DEATH(0)=SEX_TYPE INC_QUINTILE RESID_GRP CCI_GRP C_YEAR_GRP LTC_MAIN_2YR OP_2YR_YN CT_2YR_YN RT_2YR_YN
/RL;WHERE &COND.; RUN;

/*Supplementary Table 2C2. Cox proportional hazard model on the factors related to survival in patients in treatment group aged 75-84*/
%LET COND= TRT_ANY_YN=1 AND E_AGE_GRP='02. 75-84';
/*단변량*/ %PHREG_UNI;
/*다변량*/
PROC PHREG DATA=DB.LTC_DUMMYDAT;
CLASS SEX_TYPE (REF='2') INC_QUINTILE(REF='5') RESID_GRP(REF='02.NON-METRO') CCI_GRP(REF='01. 0') 
C_YEAR_GRP(REF="01. 2009-2011") LTC_MAIN_2YR (REF='04. NONE') OP_2YR_YN(REF='0') CT_2YR_YN(REF='0') RT_2YR_YN(REF='0');
MODEL SURVTIME*DEATH(0)=SEX_TYPE INC_QUINTILE RESID_GRP CCI_GRP C_YEAR_GRP LTC_MAIN_2YR OP_2YR_YN CT_2YR_YN RT_2YR_YN
/RL;WHERE &COND.; RUN;

/*Supplementary Table 2C3. Cox proportional hazard model on the factors related to survival in patients in treatment group aged 85+*/
%LET COND= TRT_ANY_YN=1 AND E_AGE_GRP='03. 85+';
/*단변량*/ %PHREG_UNI;
/*다변량*/
PROC PHREG DATA=DB.LTC_DUMMYDAT;
CLASS SEX_TYPE (REF='2') INC_QUINTILE(REF='5') RESID_GRP(REF='02.NON-METRO') CCI_GRP(REF='01. 0') 
C_YEAR_GRP(REF="01. 2009-2011") LTC_MAIN_2YR (REF='04. NONE') OP_2YR_YN(REF='0') CT_2YR_YN(REF='0') RT_2YR_YN(REF='0');
MODEL SURVTIME*DEATH(0)=SEX_TYPE INC_QUINTILE RESID_GRP CCI_GRP C_YEAR_GRP LTC_MAIN_2YR OP_2YR_YN CT_2YR_YN RT_2YR_YN
/RL;WHERE &COND.; RUN;

/*Supplementary Table 2C4. Cox proportional hazard model on the factors related to survival in patients in no-treatment group aged 65-74*/
%LET COND= TRT_ANY_YN=0 AND E_AGE_GRP='01. 65-74';
/*단변량*/ %PHREG_UNI;
/*다변량*/
PROC PHREG DATA=DB.LTC_DUMMYDAT;
CLASS SEX_TYPE (REF='2') INC_QUINTILE(REF='5') RESID_GRP(REF='02.NON-METRO') CCI_GRP(REF='01. 0') 
C_YEAR_GRP(REF="01. 2009-2011") LTC_MAIN_2YR (REF='04. NONE') OP_2YR_YN(REF='0') CT_2YR_YN(REF='0') RT_2YR_YN(REF='0');
MODEL SURVTIME*DEATH(0)=SEX_TYPE INC_QUINTILE RESID_GRP CCI_GRP C_YEAR_GRP LTC_MAIN_2YR OP_2YR_YN CT_2YR_YN RT_2YR_YN
/RL;WHERE &COND.; RUN;

/*Supplementary Table 2C5. Cox proportional hazard model on the factors related to survival in patients in no-treatment group aged 75-84*/
%LET COND= TRT_ANY_YN=0 AND E_AGE_GRP='02. 75-84';
/*단변량*/ %PHREG_UNI;
/*다변량*/
PROC PHREG DATA=DB.LTC_DUMMYDAT;
CLASS SEX_TYPE (REF='2') INC_QUINTILE(REF='5') RESID_GRP(REF='02.NON-METRO') CCI_GRP(REF='01. 0') 
C_YEAR_GRP(REF="01. 2009-2011") LTC_MAIN_2YR (REF='04. NONE') OP_2YR_YN(REF='0') CT_2YR_YN(REF='0') RT_2YR_YN(REF='0');
MODEL SURVTIME*DEATH(0)=SEX_TYPE INC_QUINTILE RESID_GRP CCI_GRP C_YEAR_GRP LTC_MAIN_2YR OP_2YR_YN CT_2YR_YN RT_2YR_YN
/RL;WHERE &COND.; RUN;

/*Supplementary Table 2C6. Cox proportional hazard model on the factors related to survival in patients in no-treatment group aged 85+*/
%LET COND= TRT_ANY_YN=0 AND E_AGE_GRP='03. 85+';
/*단변량*/ %PHREG_UNI;
/*다변량*/
PROC PHREG DATA=DB.LTC_DUMMYDAT;
CLASS SEX_TYPE (REF='2') INC_QUINTILE(REF='5') RESID_GRP(REF='02.NON-METRO') CCI_GRP(REF='01. 0') 
C_YEAR_GRP(REF="01. 2009-2011") LTC_MAIN_2YR (REF='04. NONE') OP_2YR_YN(REF='0') CT_2YR_YN(REF='0') RT_2YR_YN(REF='0');
MODEL SURVTIME*DEATH(0)=SEX_TYPE INC_QUINTILE RESID_GRP CCI_GRP C_YEAR_GRP LTC_MAIN_2YR OP_2YR_YN CT_2YR_YN RT_2YR_YN
/RL;WHERE &COND.; RUN;






/*참고분석*/

/*진단 후 2년 내 신청건 중 서로 다른 등급 판정을 받은 경우가 있는가 : 전체 등급*/
PROC SQL; CREATE TABLE TARGET_LTC_TBNYBASE_2YR AS SELECT A.*
FROM DB.TARGET_LTC_TBNYBASE AS A INNER JOIN LTC_DUMMY1 AS B
ON A.INDI_DSCM_NO=B.INDI_DSCM_NO
WHERE A.APLY_YMD^=. AND B.C_DATE<=A.APLY_YMD AND A.APLY_YMD2<= INTNX('YEAR',B.C_DATE,2,'S') 
AND GJU_RCGT_GRADE_CD IN ('1','2','3','4','5','6')
; QUIT;

PROC SQL; CREATE TABLE LTC_GRD AS /*장기요양서비스 이용자만*/
SELECT INDI_DSCM_NO, COUNT(DISTINCT INDI_SEQ) AS CNT_SEQ, COUNT(DISTINCT GJU_RCGT_GRADE_CD) AS CNT_GRD
FROM TARGET_LTC_TBNYBASE_2YR 
GROUP BY INDI_DSCM_NO; QUIT;

PROC SQL; CREATE TABLE LTC_GRD2 AS /*암 환자 전체 테이블이랑 합침*/
SELECT A.INDI_DSCM_NO, A.AGE_GRP, A.C_YEAR, A.PRE_C, B.CNT_SEQ, B.CNT_GRD
FROM LTC_DUMMY1 AS A LEFT JOIN LTC_GRD AS B
ON A.INDI_DSCM_NO=B.INDI_DSCM_NO ORDER BY INDI_DSCM_NO;QUIT;

PROC FREQ DATA=LTC_GRD2;TABLES CNT_SEQ CNT_GRD ; RUN;
PROC FREQ DATA=LTC_GRD2;TABLES CNT_SEQ CNT_GRD ; 
WHERE AGE_GRP>=14 AND C_YEAR>=2009 AND PRE_C=0 AND CNT_GRD^=0; RUN;

/*2개이상인 사람 어떤지 보자*/
DATA GRD_MULTIYN(KEEP=INDI_DSCM_NO MULTI); SET LTC_GRD; IF CNT_GRD>1 THEN MULTI=1; ELSE MULTI=0;RUN;
PROC SORT DATA=TARGET_LTC_TBNYBASE_2YR; BY INDI_DSCM_NO INDI_SEQ; RUN;
DATA GRD_2UP_1ST; SET TARGET_LTC_TBNYBASE_2YR; IF FIRST.INDI_DSCM_NO; BY INDI_DSCM_NO; RUN;
DATA GRD_2UP_1ST(KEEP=INDI_DSCM_NO FIRST_GRD); SET GRD_2UP_1ST; FIRST_GRD = GJU_RCGT_GRADE_CD; RUN;
DATA GRD_2UP_LAST; SET TARGET_LTC_TBNYBASE_2YR; IF LAST.INDI_DSCM_NO; BY INDI_DSCM_NO; RUN;
DATA GRD_2UP_LAST(KEEP=INDI_DSCM_NO LAST_GRD); SET GRD_2UP_LAST; LAST_GRD = GJU_RCGT_GRADE_CD; RUN;
PROC SQL; CREATE TABLE GRD_2UP_MAXMIN AS 
SELECT INDI_DSCM_NO, MAX(GJU_RCGT_GRADE_CD) AS MAX_GRD, MIN(GJU_RCGT_GRADE_CD) AS MIN_GRD
FROM TARGET_LTC_TBNYBASE_2YR GROUP BY INDI_DSCM_NO ORDER BY INDI_DSCM_NO ; QUIT;
DATA GRD_MERGE; MERGE GRD_MULTIYN GRD_2UP_1ST GRD_2UP_LAST GRD_2UP_MAXMIN; BY INDI_DSCM_NO;RUN;

PROC FREQ  DATA=GRD_MERGE; TABLES FIRST_GRD LAST_GRD MAX_GRD MIN_GRD; WHERE MULTI=1;RUN;

DATA TEST; SET GRD_MERGE; IF FIRST_GRD>LAST_GRD THEN TYPE=1; 
IF FIRST_GRD=LAST_GRD THEN TYPE=2; IF FIRST_GRD<LAST_GRD THEN TYPE=3;RUN;
PROC FREQ  DATA=TEST; TABLES TYPE;WHERE MULTI=1;RUN; /*등급 높아진 경우 64.54%, 낮아진 경우 33.17%*/


/*휴지통*/
/*2개이상인 사람 어떤지 보자*/
PROC SQL; CREATE TABLE DISTIN_GRD_2UP AS SELECT A.* 
FROM TARGET_LTC_TBNYBASE_2YR AS A INNER JOIN LTC_GRD2 AS B
ON A.INDI_DSCM_NO=B.INDI_DSCM_NO WHERE B.CNT_GRD>1 ORDER BY INDI_DSCM_NO, INDI_SEQ;QUIT;

DATA GRD_2UP_1ST; SET DISTIN_GRD_2UP; IF FIRST.INDI_DSCM_NO; BY INDI_DSCM_NO; RUN;
DATA GRD_2UP_1ST(KEEP=INDI_DSCM_NO FIRST_GRD); SET GRD_2UP_1ST; FIRST_GRD = GJU_RCGT_GRADE_CD; RUN;
DATA GRD_2UP_LAST; SET DISTIN_GRD_2UP; IF LAST.INDI_DSCM_NO; BY INDI_DSCM_NO; RUN;
DATA GRD_2UP_LAST(KEEP=INDI_DSCM_NO LAST_GRD); SET GRD_2UP_LAST; LAST_GRD = GJU_RCGT_GRADE_CD; RUN;
PROC SQL; CREATE TABLE GRD_2UP_MAXMIN AS 
SELECT INDI_DSCM_NO, MAX(GJU_RCGT_GRADE_CD) AS MAX_GRD, MIN(GJU_RCGT_GRADE_CD) AS MIN_GRD
FROM DISTIN_GRD_2UP GROUP BY INDI_DSCM_NO ORDER BY INDI_DSCM_NO ; QUIT;
DATA GRD_MERGE; MERGE GRD_2UP_1ST GRD_2UP_LAST GRD_2UP_MAXMIN; BY INDI_DSCM_NO;RUN;

PROC FREQ  DATA=GRD_MERGE; TABLES FIRST_GRD LAST_GRD MAX_GRD MIN_GRD;RUN;




